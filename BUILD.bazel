load("@aspect_rules_js//js:defs.bzl", "js_library")
load("@aspect_rules_js//js/private:expand_template.bzl", "expand_template")
load("@aspect_rules_js//npm:defs.bzl", "npm_package")
load("@aspect_rules_ts//ts:defs.bzl", "ts_config")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//:vsce/package_json.bzl", vsce_bin = "bin")
load(":version.bzl", "VERSION")

npm_link_all_packages(name = "node_modules")

js_library(
    name = "package_json",
    srcs = ["package.json"],
    visibility = ["//syntaxes/test:__pkg__"],
)

ts_config(
    name = "tsconfig",
    src = "tsconfig.json",
    visibility = ["//visibility:public"]
)

expand_template(
    name = "package_json_expanded",
    template = "package.json",
    out = "package_expanded.json",
    substitutions = {
        "0.0.0-PLACEHOLDER": VERSION,
        "./dist/client/src/extension": "./index",
    }
)

npm_package(
    name = "vsix_sandbox",
    srcs = [
        "package_expanded.json",
        "angular.png",
        "CHANGELOG.md",
        "README.md",
        "//client:index.js",
        "//server:npm_files",
        "//syntaxes:npm_files",
        "//v12_language_service:npm_files",
        # Transitive closure of npm deps needed for vsce; this set was determined manually by
        # running `bazel build //:vsix` and burning down missing packages. We do this so that we
        # don't have to run an additional `npm install` action to create a node_modules within the
        # //:npm npm_package where the vsce build takes place.
        ":node_modules/@angular/language-service",
        ":node_modules/balanced-match",
        ":node_modules/brace-expansion",
        ":node_modules/concat-map",
        ":node_modules/lru-cache",
        ":node_modules/minimatch",
        ":node_modules/semver",
        ":node_modules/typescript",
        ":node_modules/vscode-jsonrpc",
        ":node_modules/vscode-languageclient",
        ":node_modules/vscode-languageserver-protocol",
        ":node_modules/vscode-languageserver-types",
        ":node_modules/vscode-languageserver",
        ":node_modules/vscode-uri",
        ":node_modules/yallist",
    ],
    include_srcs_packages = [
        "**",
    ],
    exclude_srcs_patterns = [
        "*.map",
        "**/*.map",
    ],
    replace_prefixes = {
        "package_expanded.json": "package.json",
        "client/": "",
        "syntaxes/src/": "syntaxes/",
    },
)

vsce_bin.vsce(
    name = "vsix",
    srcs = [
        ":vsix_sandbox",
    ],
    outs = ["ng-template-{}.vsix".format(VERSION)],
    chdir = "vsix_sandbox",
    args = [
        "package",
        "-o",
        "../ng-template-{}.vsix".format(VERSION)
    ],
    # vsce requires npm on the PATH; we can get this from the Bazel rules_nodejs but it is not
    # included by default in rules_js binary rules so we include it here explicitly
    include_npm = True,
)

npm_package(
    name = "npm",
    srcs = [
        ":vsix_sandbox",
        ":vsix",
    ],
    root_paths = [
        "vsix_sandbox"
    ],
    visibility = ["//integration:__subpackages__"],
)
